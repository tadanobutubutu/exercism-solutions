defmodule AllYourBase do
  @doc """
  Given a number in input base, represented as a sequence of digits, converts it to output base,
  or returns an error tuple if either of the bases are less than 2
  """

  @spec convert(list, integer, integer) :: {:ok, list} | {:error, String.t()}
  def convert(digits, input_base, output_base) do
    cond do
      input_base < 2 ->
        {:error, "input base must be >= 2"}

      output_base < 2 ->
        {:error, "output base must be >= 2"}

      not Enum.all?(digits, &(&1 >= 0 and &1 < input_base)) ->
        {:error, "all digits must be >= 0 and < input base"}

      true ->
        value = Enum.reduce(digits, 0, &(&2 * input_base + &1))
        {:ok, to_digits(value, output_base, [])}
    end
  end

  defp to_digits(0, _base, []), do: [0]
  defp to_digits(0, _base, digits), do: digits

  defp to_digits(value, base, digits) do
    to_digits(div(value, base), base, [rem(value, base) | digits])
  end
end
